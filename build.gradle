plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '1.2.3'
    id 'findbugs'
}

group 'org.kelog'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

def mainClass = 'facerec.Main'

clean.doFirst {
    delete "${rootDir}/core/bin"
}

task compileCpp(type: Exec) {
    workingDir './core'
    commandLine 'make'
    inputs.dir './core/src'
    outputs.dir './core/bin'
}

tasks.compileJava.dependsOn(compileCpp)

task train(type: Exec) {
    workingDir './core/bin'
    commandLine './train', '../photos'
    inputs.dir './core/photos'
    outputs.dir './core/models'
}

tasks.train.dependsOn(compileCpp)
tasks.compileJava.dependsOn(train)

task bowerInstall(type: Exec) {
    workingDir './public'
    commandLine 'bower', 'install'
    inputs.file './public/bower.json'
    outputs.dir './public/bower_components'
}

tasks.compileJava.dependsOn(bowerInstall)

task run(type: JavaExec) {
    main = mainClass
    classpath = sourceSets.main.runtimeClasspath
}

jar {
    manifest {
        attributes(
                'Main-Class': mainClass
        )
    }
}

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}


dependencies {
    compile 'com.sparkjava:spark-core:2.3'
    compile 'com.google.code.gson:gson:2.6.2'
    compile 'org.apache.commons:commons-io:1.3.2'
    compile group: 'org.apache.commons', name: 'commons-configuration2', version: '2.0'
    compile group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.2'
}
